#pragma kernel CSMain

RWTexture2D<float4> Output;

int NumTextures;

Texture2D<float4> Input[8];
#ifndef SHADER_API_GLES3
SamplerState samplerInput[8];
#endif

float4 Opacities[2];
float4 OffsetsAndScales[8];

[numthreads(8, 8, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
	uint width, height;
	Output.GetDimensions(width, height);

	if (id.x < width && id.y < height)
	{
		float3 output = 1.0f;
		float2 uv = float2((id.x + 0.5f) / width, (id.y + 0.5f) / height);

		for (int i = 0; i < NumTextures; i++)
		{
#ifndef SHADER_API_GLES3
			float4 texSample = Input[i].SampleLevel(samplerInput[i], uv * OffsetsAndScales[i].z + OffsetsAndScales[i].xy, 0);
#else
			int2 sampleCoord = int2((uv * OffsetsAndScales[i].z + OffsetsAndScales[i].xy) * float2(width, height));
			float4 texSample = Input[i][sampleCoord];
#endif

			output = lerp(output, texSample.rgb, Opacities[i / 4][i % 4] * texSample.a);
		}

		Output[id.xy] = float4(output, 1.0f);
	}
}
